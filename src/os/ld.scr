OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH( h8300h )
ENTRY("_start")

MEMORY
{
	ramall(rwx) 	: o = 0xffbf20, l = 0x004000 /* 16KB */
	softvec		: o = 0xffbf20, l = 0x000040 /* top of RAM = ISR */
	ram(rwx)	: o = 0xffc020,	l = 0x003f00
	stack(rw)	: o = 0xffff00, l = 0x000000 /* End of RAM */
	userstack(rw)	: o = 0xfff400,	l = 0x000000 /* User Stack */
	bootstack(rw)	: o = 0xffff00, l = 0x000000 /* Boot Stack */
	intrstack(rw)	: o = 0xffff00, l = 0x000000 /* Interrupt Stack */
	sci0		: o = 0xffffb0, l = 0x000000 /* Serial port 0 */
	sci1		: o = 0xffffb8, l = 0x000000 /* Serial port 1 */
	sci2		: o = 0xffffc0, l = 0x000000 /* Serial port 2 */
}

SECTIONS
{
	.text : {
		_text_start = .;
		*(.text)
		_etext = .;
	} > ram

	.softvec : {
		 _softvec = .;
	} > softvec

	.rodata : {
		_rodata_start = .;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = .;
	} > ram

	.data : {
		_data_start = .;
		*(.data)
		_edata = .;
	} > ram

	.bss : {
		_bss_start = .;
		*(.bss)
		*(COMMON)
		_ebss = .;
	} > ram

	. = ALIGN(4);
	_end = .;

	.freearea : {
		_freearea = .;
	} > ram

	.userstack : {
		_userstack = .;
	} > userstack

	.bootstack : {
		_bootstack = .;
	} > bootstack

	.intrstack : {
		_intrstack = .;
	} > intrstack
	
	.sci0 : {
	      	_sci0_ptr = .;
	} > sci0

}

